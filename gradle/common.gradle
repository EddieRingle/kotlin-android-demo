ext.ver = [
    app     : "0.1.0",
    appCode : 1,
    android : [
        buildTools: "24.0.2",
        gradle    : "2.2.0",
        min       : 21,
        sdk       : 24,
        support   : "24.2.1",
    ],
    anko    : "0.9",
    dagger  : "2.7",
    dexcount: "0.6.1",
    jacoco  : "0.7.7.201606060606",
    java    : JavaVersion.VERSION_1_7,
    kotlin  : "1.1.0-dev-+",
    rx      : [
        java     : "1.2.0",
        lifecycle: "0.6.1",
    ],
    stetho  : "1.3.1",
    test    : [
        amock  : "1.0.0",
        android: [
            dexmaker: "a766570",
            espresso: "2.2.2",
            runner  : "0.5",
        ],
        junit  : "4.12",
    ]
]

ext.dep = [
    android: [
        annotations : "com.android.support:support-annotations:${ver.android.support}",
        appcompat   : "com.android.support:appcompat-v7:${ver.android.support}",
        design      : "com.android.support:design:${ver.android.support}",
        gridlayout  : "com.android.support:gridlayout-v7:${ver.android.support}",
        recyclerview: "com.android.support:recyclerview-v7:${ver.android.support}",
        supportV4   : "com.android.support:support-v4:${ver.android.support}",
    ],
    anko   : [
        appcompat   : "org.jetbrains.anko:anko-appcompat-v7:${ver.anko}",
        common      : "org.jetbrains.anko:anko-common:${ver.anko}",
        design      : "org.jetbrains.anko:anko-design:${ver.anko}",
        gridlayout  : "org.jetbrains.anko:anko-gridlayout-v7:${ver.anko}",
        recyclerview: "org.jetbrains.anko:anko-recyclerview-v7:${ver.anko}",
        sdk         : "org.jetbrains.anko:anko-sdk23:${ver.anko}",
    ],
    apt    : [
        dagger: "com.google.dagger:dagger-compiler:${ver.dagger}",
    ],
    dagger : "com.google.dagger:dagger:${ver.dagger}",
    inject : "javax.inject:javax.inject:1",
    kotlin : [
        reflect: "org.jetbrains.kotlin:kotlin-reflect:${ver.kotlin}",
        runtime: "org.jetbrains.kotlin:kotlin-runtime:${ver.kotlin}",
        stdlib : "org.jetbrains.kotlin:kotlin-stdlib:${ver.kotlin}",
    ],
    rx     : [
        java     : "io.reactivex:rxjava:${ver.rx.java}",
        lifecycle: "com.trello:rxlifecycle:${ver.rx.lifecycle}",
    ],
    stetho : [
        core: "com.facebook.stetho:stetho:${ver.stetho}",
    ],
    test   : [
        amock  : "com.oneeyedmen:amock:${ver.test.amock}",
        android: [
            dexmaker: "com.github.eddieringle.dexmaker:dexmaker:${ver.test.android.dexmaker}",
            espresso: [
                core   : "com.android.support.test.espresso:espresso-core:${ver.test.android.espresso}",
                contrib: "com.android.support.test.espresso:espresso-contrib:${ver.test.android.espresso}",
                intents: "com.android.support.test.espresso:espresso-intents:${ver.test.android.espresso}",
            ],
            rules   : "com.android.support.test:rules:${ver.test.android.runner}",
            runner  : "com.android.support.test:runner:${ver.test.android.runner}",
        ],
        junit  : "junit:junit:${ver.test.junit}",
    ]
]

ext.plug = [
    android     : "com.android.tools.build:gradle:${ver.android.gradle}",
    dexcount    : "com.getkeepsafe.dexcount:dexcount-gradle-plugin:${ver.dexcount}",
    jacoco      : "org.jacoco:org.jacoco.core:${ver.jacoco}",
    jacocoAgent : "org.jacoco:org.jacoco.agent:${ver.jacoco}",
    jacocoReport: "org.jacoco:org.jacoco.report:${ver.jacoco}",
    kotlin      : "org.jetbrains.kotlin:kotlin-gradle-plugin:${ver.kotlin}"
]

ext.commonRepos = {
  jcenter()
  maven { url "https://dl.bintray.com/kotlin/kotlin-dev" }
  maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
  maven { url "https://jitpack.io" }
}

rootProject.buildscript {
  repositories.configure commonRepos
  dependencies {
    plug.each { k, v ->
      classpath v
    }
  }
}

allprojects {
  repositories.configure commonRepos
}
